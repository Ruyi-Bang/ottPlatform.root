plugins {
    id 'java-platform'
    id 'maven-publish'
}

group = 'RuyiBang'
version = '1.0.X'

// Define dependency and plugin versions centrally
javaPlatform {
    allowDependencies()
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    plugins.withId("java") {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(21)
            }
        }
    }
}

dependencies {
    // Import BOMs
    api platform("org.springframework.boot:spring-boot-dependencies:${libs.versions.springBoot.get()}")

    // Example managed libs
    constraints {
        api "io.jsonwebtoken:jjwt-api:${libs.versions.jjwt.get()}"
        api "io.jsonwebtoken:jjwt-impl:${libs.versions.jjwt.get()}"
        api "io.jsonwebtoken:jjwt-jackson:${libs.versions.jjwt.get()}"
        api "org.postgresql:postgresql:${libs.versions.postgres.get()}"
        api "ch.qos.logback:logback-classic:${libs.versions.logback.get()}"
        api "ch.qos.logback:logback-core:${libs.versions.logback.get()}"
        api "net.logstash.logback:logstash-logback-encoder:${libs.versions.logstashEncoder.get()}"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.javaPlatform
        }
    }
}